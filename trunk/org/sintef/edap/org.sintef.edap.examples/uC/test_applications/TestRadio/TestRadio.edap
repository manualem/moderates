import "../../devices/Button.edap"
import "../../devices/Led.edap"
import "../../devices/Timer.edap"
import "../../devices/RF2500.edap"

component TestRadio {

	// Define the devices used by the application
	property leds : LED[2..2]
	property button : Button[1..1]
	property timers : Timer[2..2]
	property rf2500 : RF2500[1..1]

	// Behaviour of the application
	statechart TestRadio init INIT {
	
		state INIT {
			on entry { 
				send Timer.start('1', '10000')
				send LED.light_on('1')
				send LED.light_on('2')
			}
			
			on exit send Timer.start('1', '10000')
			
			transition run -> Running {
				event Timer#timeout
				guard 'timer_id == 1'
				action {
					send LED.light_off('1')
					send LED.light_off('2')
				}
			}
		}
	
		state Running {
			
			transition reception -> Running {
				event RF2500#receive
				guard 'strcmp(msg, "HELLO")'
				action {
					send LED.toggle('2')
				}
			}
			
			transition radio_send -> Running {
				event Timer#timeout
				guard 'timer_id == 1'
				action {
					send LED.toggle('1')
					send RF2500.transmit('0', '"HELLO"')
					send Timer.start('1', '10000')
				}
			}
		}
		
	}
}
